﻿title: DeepSubmergence_Diver_Root
---
<<if GetHasVisitedNode("DeepSubmergence_Diver_Quest0") == false>>
	<<jump DeepSubmergence_Diver_Quest0>>
<<endif>>
===
﻿title: DeepSubmergence_Diver_Chat
---
<<AutoResolveNextLine>>
deepsubmergence.diver: ... #line:deepsubmergence.chatdialogue0
<<jump DeepSubmergence_Diver_QuestRoot>>
===
title: DeepSubmergence_Diver_QuestRoot
---
<<if GetIntVariable("deepsubmergence.questprogress") == 0>>
	<<AutoResolveNextLine>>
	deepsubmergence.diver: There was a small bolting fish, a squid caught in a net, and a shoal... so many hungry mouths. #line:deepsubmergence.quest0dialoguerepeat
	<<ShowQuestGrid deepsubmergence.quest0requiredfish>>
	<<if GetLastQuestGridResult() == 1>>
		<<jump DeepSubmergence_Diver_Quest1>>
	<<endif>>
<<elseif GetIntVariable("deepsubmergence.questprogress") == 1>>
	<<AutoResolveNextLine>>
	deepsubmergence.diver: A wriggling shock... a glowing crawler... a bleeding slice of iron... and a needle’s tip. So sharp... so sharp... #line:deepsubmergence.quest1dialoguerepeat
	<<ShowQuestGrid deepsubmergence.quest1requiredfish>>
	<<if GetLastQuestGridResult() == 1>>
		<<HidePortrait>>
		<<jump DeepSubmergence_Diver_Quest2>>
	<<endif>>
<<elseif GetIntVariable("deepsubmergence.questprogress") == 2>>
	<<AutoResolveNextLine>>
	deepsubmergence.diver: A fish with an battered mask. An explosive thing, stitched in agony. A crushing maw... shredding... tearing. #line:deepsubmergence.quest2dialoguerepeat
	<<ShowQuestGrid deepsubmergence.quest2requiredfish>>
	<<if GetLastQuestGridResult() == 1>>
		<<HidePortrait>>
		<<jump DeepSubmergence_Diver_Quest3>>
	<<endif>>
<<elseif GetIntVariable("deepsubmergence.questprogress") == 3>>
	<<AutoResolveNextLine>>
	deepsubmergence.diver: One more. A bleached machine to cut and break through. Gnawing at the shell of reality. #line:deepsubmergence.quest3dialoguerepeat
	<<ShowQuestGrid deepsubmergence.quest3requiredfish>>
	<<if GetLastQuestGridResult() == 1>>
		<<HidePortrait>>
		<<jump DeepSubmergence_Diver_QuestDone>>
	<<endif>>
<<endif>>
===
﻿title: DeepSubmergence_Diver_Quest0
---
<<SetIntVariable deepsubmergence.questprogress 0>>
<<ShowPortrait deepsubmergence.diver>>
<<SetSpeakerAvailability deepsubmergence.diver true>>
<<SetDestinationAvailable deepsubmergence.seabaseshop false>>
deepsubmergence.diver: Ah... what ill fate dragged you to these depths, to this... place? #aah #line:deepsubmergence.quest0dialogue0
deepsubmergence.diver: But, no matter. I need something from you. A favor, if you will. #line:deepsubmergence.quest0dialogue1
deepsubmergence.diver: I was stationed here, once, until some of my research... slipped away. #line:deepsubmergence.quest0dialogue2
<<AutoResolveNextLine>>
deepsubmergence.diver: There was a small bolting fish, a squid caught in a net, and a shoal... so many hungry mouths. #line:deepsubmergence.quest0dialogue3
<<ShowQuestGrid deepsubmergence.quest0requiredfish>>
<<if GetLastQuestGridResult() == 1>>
	<<jump DeepSubmergence_Diver_Quest1>>
<<endif>>
deepsubmergence.diver: Don’t fret. They won't rot. They never rot. #line:deepsubmergence.quest0dialogue4
deepsubmergence.diver: Keep them... close... until you bring them to me. Return here when you have them all... and... everything will be... #line:deepsubmergence.quest0dialogue5
===
﻿title: DeepSubmergence_Diver_Quest1
---
<<SetIntVariable deepsubmergence.questprogress 1>>
<<ShowPortrait deepsubmergence.diver>>
<<SetDestinationAvailable deepsubmergence.seabaseshop true>>
deepsubmergence.diver: That's... that's them. You-you found them. But how did...? #line:deepsubmergence.quest1dialogue0
deepsubmergence.diver: When they... when they sent me down here, I was... elated. So elated. They'd given me... everything I wanted #line:deepsubmergence.quest1dialogue1
deepsubmergence.diver: Free reign... to test... to discover... to create... #line:deepsubmergence.quest1dialogue2
deepsubmergence.diver: Take this. Here. Blueprints... for reinforcing your hull. For... for pushing out the water. The water that seeps in when you dive too deep. #line:deepsubmergence.quest1dialogue3
deepsubmergence.diver: But... but this isn’t everything. No... no, there’s more. More out there... #line:deepsubmergence.quest1dialogue4
<<AutoResolveNextLine>>
deepsubmergence.diver: A wriggling shock... a glowing crawler... a bleeding slice of iron... and a needle’s tip. So sharp... so sharp... #line:deepsubmergence.quest1dialogue5
<<ShowQuestGrid deepsubmergence.quest1requiredfish>>
<<if GetLastQuestGridResult() == 1>>
	<<HidePortrait>>
	<<jump DeepSubmergence_Diver_Quest2>>
<<endif>>
===
﻿title: DeepSubmergence_Diver_Quest2
---
<<SetIntVariable deepsubmergence.questprogress 2>>
<<ShowPortrait deepsubmergence.diver>>
deepsubmergence.diver: Take it. More blueprints. Deeper... always deeper... ever deeper. #line:deepsubmergence.quest2dialogue0
deepsubmergence.diver: They warned us, didn’t they? You know, you remember. #line:deepsubmergence.quest2dialogue1
deepsubmergence.diver: Alone, down here. Alone. No one to watch, to track... to make sure... To check on. #line:deepsubmergence.quest2dialogue2
deepsubmergence.diver: It was just a thought. A research grant, they said. Research. Endless hours... smearing... into forever. #line:deepsubmergence.quest2dialogue3
deepsubmergence.diver: More. I need more. #line:deepsubmergence.quest2dialogue4
<<AutoResolveNextLine>>
deepsubmergence.diver: A fish with an battered mask. An explosive thing, stitched in agony. A crushing maw... shredding... tearing. #line:deepsubmergence.quest2dialogue5
<<ShowQuestGrid deepsubmergence.quest2requiredfish>>
<<if GetLastQuestGridResult() == 1>>
	<<HidePortrait>>
	<<jump DeepSubmergence_Diver_Quest3>>
<<endif>>
===
﻿title: DeepSubmergence_Diver_Quest3
---
<<SetIntVariable deepsubmergence.questprogress 3>>
<<ShowPortrait deepsubmergence.diver>>
deepsubmergence.diver: Yes, yes, these. My children, almost perfect, almost ready for providence. #line:deepsubmergence.quest3dialogue0
deepsubmergence.diver: Take these blueprints and dive, deeper. We're so close to touching it. Touching the abyss. #line:deepsubmergence.quest3dialogue1
deepsubmergence.diver: The agency, said no, it's not there, it's not under, you CAN'T GO THERE! #line:deepsubmergence.quest3dialogue2
<<AutoResolveNextLine>>
deepsubmergence.diver: ... #line:deepsubmergence.quest3dialogue3
deepsubmergence.diver: One more. #line:deepsubmergence.quest3dialogue4
deepsubmergence.diver: A bleached machine to cut and break through. Gnawing at the shell of reality. #line:deepsubmergence.quest3dialogue5
<<ShowQuestGrid deepsubmergence.quest3requiredfish>>
<<if GetLastQuestGridResult() == 1>>
	<<HidePortrait>>
	<<jump DeepSubmergence_Diver_QuestDone>>
<<endif>>
===
﻿title: DeepSubmergence_Diver_QuestDone
---
<<SetIntVariable deepsubmergence.questprogress 4>>
<<ShowPortrait deepsubmergence.diver>>
<<SetSpeakerAvailability deepsubmergence.diver false>>
<<AutoResolveNextLine>>
deepsubmergence.diver: ... #line:deepsubmergence.questdonedialogue0
The corpse twitches and siezes, like machinery. Was it ever even alive? #line:deepsubmergence.questdonedialogue1
===
﻿title: DeepSubmergence_Diver_Shop
---
===